/*
         __     _    _
 _   _  / _|   | |_ (_) _ __ ___    ___  _ __
| | | || |_    | __|| || '_ ` _ \  / _ \| '__|
| |_| ||  _| _ | |_ | || | | | | ||  __/| |
 \__,_||_|  (_) \__||_||_| |_| |_| \___||_|

*/

#include "totvs.ch"
#include "parmtype.ch"
#include "tlpp-core.th"

namespace uf
using namespace uf

static __oUFTTimer as object

class Timer

    static method TimerSleep(nSleep as numeric,nWaits as numeric,bConfirmWait as codeblock) as logical

    static method Activate(nInterval as numeric,bAction as codeblock,oDlg as object) as object
    static method DeActivate() as logical

end class

static method TimerSleep(nSleep,nWaits,bConfirmWait) class timer

    local lConfirmWait:=.F. as logical

    local nWait:=0 as numeric

    paramtype 1 var nSleep as numeric optional default 1000
    paramtype 2 var nWaits as numeric optional default 15
    paramtype 3 var bConfirmWait as block optional default {||.F.}

    while (((!KillApp()).and.(++nWait)<nWaits))
        //Aguarda nSleep milisegundos de no maximo nWaits
        Sleep(nSleep)
        lConfirmWait:=Eval(bConfirmWait)
        if (lConfirmWait)
            exit
        endif
    end while

return(lConfirmWait)

static method Activate(nInterval,bAction,oDlg) class Timer

    local lUFTTimer as logical

    paramtype 1 var nInterval as numeric optional default 1000
    paramtype 2 var bAction as block optional default {||if((Type("__oUFTTimer")!="O"),.F.,Timer():DeActivate())}
    paramtype 3 var oDlg as object optional default GetWndDefault()

    lUFTTimer:=(Type("__oUFTTimer")=="O")
    if (!lUFTTimer)
        __oUFTTimer:=TTimer():New(nInterval,bAction,oDlg)
    endif

    if (lUFTTimer)
        __oUFTTimer:DeActivate()
    endif

    __oUFTTimer:bAction:=bAction

    __oUFTTimer:Activate()

return(__oUFTTimer)

static method DeActivate() class Timer

    local lUFTTimer as logical

    lUFTTimer:=(Type("__oUFTTimer")=="O")
    if (lUFTTimer)
        __oUFTTimer:DeActivate()
        FreeObj(@__oUFTTimer)
        DelClassIntF()
        lUFTTimer:=(Type("__oUFTTimer")=="O")
    endif

return(lUFTTimer)
