/*----------------------------------------------------------------------
       _                  _
  ___ | |_   ___    ___  | | ___
 / __|| __| / _ \  / _ \ | |/ __|
| (__ | |_ | (_) || (_) || |\__ \
 \___| \__| \___/  \___/ |_||___/

    The source code `uf.ctools.tlpp` is based on a more detailed and feature-rich
    source code, `dna.tech.ctools.tlpp`, and implements part of the functionalities
    present in the latter.

*/

#include "totvs.ch"
#include "parmtype.ch"

namespace uf
using namespace uf

static __aAccentuation as array
static __nAccentuation as numeric

class cTools

    static method RetPictVal(nVal as numeric,lDecZero as logical,nInt as numeric,nDec as numeric,lPictSepMil as logical) as character
    static method TextRuler(cText as character,lStep10 as logical) as character

    static method StrTokArray(cString as character,cToken as character,bEvalToken as codeblock,aStrTokArr as array) as array
    static method __StrTokArray(cString as character,cToken as character,bEvalToken as codeblock,aStrTokArr as array) as array

    static method StrTokCount(cString as character,cToken as character,bEvalToken as codeblock) as numeric
    static method __StrTokCount(cString as character,cToken as character,bEvalToken as codeblock) as numeric

    static method StrTokCountArray(cString as character,cToken as character,bEvalToken as codeblock) as array
    static method __StrTokCountArray(cString as character,cToken as character,bEvalToken as codeblock) as array

    static method Accentuation(cString as character,vAccentuation as variadic) as character
    static method WebAccentuation(cString as character) as character

end class

static method RetPictVal(nVal,lDecZero,nInt,nDec,lPictSepMil) class cTools

    local cPictVal as character

    paramtype nVal as numeric
    paramtype lDecZero as logical optional
    paramtype nInt as numeric optional
    paramtype nDec as numeric optional
    paramtype lPictSepMil as logical optional

    cPictVal:=RetPictVal(@nVal,@lDecZero,@nInt,@nDec,@lPictSepMil)

    if (Right(cPictVal,2)==".9")
        cPictVal+="9"
    endif

    if (Left(cPictVal,1)==",")
        cPictVal:=SubStr(cPictVal,2)
    endif

    return(cPictVal)

static function RetPictVal(nVal as numeric,lDecZero as logical,nInt as numeric,nDec as numeric,lPictSepMil as logical) as character

    local cPict as character
    local cPictSepMil as character

    local uInt
    local uDec

    uInt:=Int(nVal)
    uDec:=(nVal-uInt)
    dfv():Default(@lDecZero,.F.)
    if ((uDec==0).and.(!(lDecZero)))
        uDec:=nil
    endif
    if (uDec<>nil)
        uDec:=allTrim(Str(uDec))
        uDec:=SubStr(uDec,At(".",uDec)+1)
        uDec:=len(uDec)
    endif
    uInt:=len(allTrim(Str(uInt)))
    nInt:=uInt
    cPict:=Replicate("9",uInt)
    dfv():Default(@lPictSepMil,.F.)
    if (lPictSepMil)
        if (nInt>3)
            cPictSepMil:=cPict
            cPict:=""
            for uInt:=nInt To 1 Step-3
                cPict:=(","+SubStr(cPictSepMil,-3,uInt)+cPict)
            next uInt
        endif
    endif
    if (uDec<>nil)
        cPict+="."
        cPict+=Replicate("9",uDec)
        nDec:=uDec
    endif

    return(cPict)

static method TextRuler(cText,lStep10) class cTools

    local cMult:="" as character
    local cRuller:="1234567890" as character
    local cTextRuler:="" as character

    local nStr as numeric
    local nStep10 as numeric

    paramtype 1 var cText as character
    paramtype 2 var lStep10 as logical optional default .T.

    nStr:=Len(cText)
    nStep10:=0

    while (Len(cTextRuler)<nStr)
        if (!lStep10)
            nStep10+=10
            if (nStep10>1000000000)
                exit
            endif
            cMult:=LTrim(Str(nStep10))
            cMult:=PadL(cMult,10)
            cTextRuler+=cMult
        else
            cTextRuler+=cRuller
        endif
    end while

    cTextRuler:=Left(cTextRuler,nStr)

return(cTextRuler)

static method StrTokArray(cString,cToken,bEvalToken,aStrTokArr) class cTools

    local lTokCount:=.F. as logical

    paramtype 1 var cString as character
    paramtype 2 var cToken as character optional default "+"
    paramtype 3 var bEvalToken as block optional
    paramtype 4 var aStrTokArr as array optional default Array(0)

    aStrTokArr:=strToKArray(@cString,@cToken,@bEvalToken,@aStrTokArr,@lTokCount)

return(aStrTokArr)

static method __StrTokArray(cString,cToken,bEvalToken,aStrTokArr) class cTools
    local lTokCount:=.F. as logical
    aStrTokArr:=strToKArray(@cString,@cToken,@bEvalToken,@aStrTokArr,@lTokCount)
return(aStrTokArr)

static method StrTokCount(cString,cToken,bEvalToken) class cTools

    local aStrTokArr:=Array(0) as array
    local lTokCount:=.T. as logical

    local nStrTokCount:=0 as numeric

    paramtype 1 var cString as character
    paramtype 2 var cToken as character optional default "+"
    paramtype 3 var bEvalToken as block optional

    nStrTokCount:=strToKArray(@cString,@cToken,@bEvalToken,@aStrTokArr,@lTokCount)

    FWFreeArray(@aStrTokArr)

return(nStrTokCount)

static method __StrTokCount(cString,cToken,bEvalToken) class cTools
    local lTokCount:=.T. as logical
    local nStrTokCount:=0 as numeric
    nStrTokCount:=strToKArray(@cString,@cToken,@bEvalToken,nil,@lTokCount)
return(nStrTokCount)

static method StrTokCountArray(cString,cToken,bEvalToken) class cTools

    local aStrTokArr as array

    local cStr as character

    local nIndex:=0 as numeric
    local nATToken as numeric
    local nRealSize as numeric
    local nStrTokCount as numeric

    paramtype 1 var cString as character
    paramtype 2 var cToken as character optional default "+"
    paramtype 3 var bEvalToken as block optional

    nStrTokCount:=cTools():StrTokCount(@cString,@cToken,@bEvalToken)
    aStrTokArr:=Array(nStrTokCount)

    nRealSize:=Len(cToken)
    while ((nATToken:=AT(cToken,cString))>0)
        cStr:=Left(cString,(nATToken-1))
        aStrTokArr[++nIndex]:=cStr
        cString:=subStr(cString,(nATToken+nRealSize))
    end while

    if (Len(cString)>0)
        aStrTokArr[++nIndex]:=cString
    endif

    cStr:=""
    cString:=""

return(aStrTokArr)

static method __StrTokCountArray(cString,cToken,bEvalToken) class cTools

    local aStrTokArr as array

    local cStr as character

    local nIndex:=0 as numeric
    local nATToken as numeric
    local nRealSize as numeric
    local nStrTokCount as numeric

    nStrTokCount:=cTools():__StrTokCount(@cString,@cToken,@bEvalToken)
    aStrTokArr:=Array(nStrTokCount)

    nRealSize:=Len(cToken)
    while ((nATToken:=AT(cToken,cString))>0)
        cStr:=Left(cString,(nATToken-1))
        aStrTokArr[++nIndex]:=cStr
        cString:=subStr(cString,(nATToken+nRealSize))
    end while

    if (Len(cString)>0)
        aStrTokArr[++nIndex]:=cString
    endif

    cStr:=""
    cString:=""

return(aStrTokArr)

static function strToKArray(cString as character,cToken as character,bEvalToken as codeblock,aStrTokArr as array,lTokCount as logical) as variant

    local cStr,cStringNew as character

    local lAdd:=.T. as logical
    local lbEvalToken:=(valType(bEvalToken)=="B") as logical

    local nATToken as numeric
    local nRealSize as numeric
    local nStrTokCount:=0 as numeric

    nRealSize:=Len(cToken)
    while ((nATToken:=AT(cToken,cString))>0)
        cStr:=Left(cString,(nATToken-1))
        if (lbEvalToken)
            lAdd:=eval(bEvalToken,@cStr)
        endif
        if (lAdd)
            if (lTokCount)
                nStrTokCount++
            else
                aAdd(aStrTokArr,cStr)
            endif
            cStringNew+=cStr
            cStringNew+=cToken
        endif
        cString:=subStr(cString,(nATToken+nRealSize))
    end while

    if (Len(cString)>0)
        cStr:=cString
        cString:=""
        if (lbEvalToken)
            lAdd:=eval(bEvalToken,@cStr)
        endif
        if (lAdd)
            if (lTokCount)
                nStrTokCount++
            else
                aAdd(aStrTokArr,cStr)
            endif
            cStringNew+=cStr
        endif
    endif

    cStr:=""
    cString:=cStringNew
    cStringNew:=""

    if (lTokCount)
        return(nStrTokCount)
    endif

return(aStrTokArr)

static function ___ctools____Accentuation(cString as character,vAccentuation as variadic) as character

    local ai18n as array

    local cIdx as character
    local cChar as character
    local cAccentuation:=cString as character

    local nAT as numeric
    local nIdx as numeric
    local nLen as numeric
    local nATPos as numeric
    local nAccentuation as numeric

    begin sequence

        if (vAccentuation==nil)
            break
        endif

        getAccentuation()
        dfv():DefaultEmpty(@__nAccentuation,len(__aAccentuation))

        ai18n:=array(0)

        for nAccentuation:=1 to vAccentuation:vCount
            cChar:=allTrim(vAccentuation:vArgs[nAccentuation])
            nAT:=aScan(__aAccentuation,{|x|x[3]==cChar})
            cChar:=if(nAT>0,__aAccentuation[nAT][1],cChar)
            aAdd(ai18n,cChar)
        next nAccentuation

        nIdx:=0
        nLen:=Len(cAccentuation)
        for nAT:=1 to nLen
            nATPos:=AT("#",SubStr(cAccentuation,nAT))
            if (nATPos==0)
                exit
            endif
            nAT+=(nATPos-1)
            cChar:=SubStr(cAccentuation,nAT,1)
            if (cChar=="#")
                cIdx:=LTrim(Str(++nIdx))
                cAccentuation:=stuff(cAccentuation,++nAT,1,cIdx)
            endif
        next nAT

        cAccentuation:=i18n(cAccentuation,ai18n)

        FWFreeArray(@ai18n)

    end sequence

    return(cAccentuation)


static method WebAccentuation(cString) class cTools

    local cStr as character
    local cAccent as character
    local cWebString as character

    local nAT as numeric

    paramtype 1 var cString as character
    cWebString:=cString

    getAccentuation()
    dfv():DefaultEmpty(@__nAccentuation,len(__aAccentuation))

    for nAT:=1 to __nAccentuation
        cStr:=__aAccentuation[nAT][1]
        cAccent:=("&"+__aAccentuation[nAT][3]+";")
        cWebString:=strTran(cWebString,cStr,cAccent)
    next nAccent

return(cWebString)

static function getAccentuation() as array

    if (empty(__aAccentuation))
        __aAccentuation:={;
            {chr(009),"","HT"},;
            {chr(010),"","LF"},;
            {chr(013),"","CR"},;
            {chr(129),"","129"},;
            {chr(141),"",""},;
            {chr(143),"","143"},;
            {chr(144),"","144"},;
            {chr(157),"","157"},;
            {chr(185),"1","sup1"},;
            {chr(186),"o","ordm"},;
            {chr(188),"1/4","frac14"},;
            {chr(189),"1/2","frac12"},;
            {chr(190),"3/4","frac34"},;
            {chr(191),"?","iquest"},;
            {chr(192),"A","Agrave"},;
            {chr(195),"A","Atilde"},;
            {chr(196),"A","Auml"},;
            {chr(197),"A","Aring"},;
            {chr(198),"AE","AElig"},;
            {chr(224),"a","agrave"},;
            {chr(225),"a","aacute"},;
            {chr(226),"a","acirc"},;
            {chr(227),"a","atilde"},;
            {chr(228),"a","auml"},;
            {chr(229),"a","aring"},;
            {chr(230),"ae","aelig"},;
            {chr(200),"E","Egrave"},;
            {chr(201),"E","Eacute"},;
            {chr(202),"E","Ecirc"},;
            {chr(203),"E","Euml"},;
            {chr(232),"e","egrave"},;
            {chr(233),"e","eacute"},;
            {chr(234),"e","ecirc"},;
            {chr(235),"e","euml"},;
            {chr(204),"I","Igrave"},;
            {chr(205),"I","Iacute"},;
            {chr(206),"I","Icirc"},;
            {chr(207),"I","Iuml"},;
            {chr(236),"i","igrave"},;
            {chr(237),"i","iacute"},;
            {chr(238),"i","icirc"},;
            {chr(239),"i","iuml"},;
            {chr(210),"O","Ograve"},;
            {chr(211),"O","Oacute"},;
            {chr(212),"O","Ocirc"},;
            {chr(213),"O","Otilde"},;
            {chr(214),"O","Ouml"},;
            {chr(242),"o","ograve"},;
            {chr(243),"o","oacute"},;
            {chr(244),"o","ocirc"},;
            {chr(245),"o","otilde"},;
            {chr(246),"o","ouml"},;
            {chr(216),"O","Oslash"},;
            {chr(217),"U","Ugrave"},;
            {chr(218),"U","Uacute"},;
            {chr(219),"U","Ucirc"},;
            {chr(220),"U","Uuml"},;
            {chr(221),"Y","Yacute"},;
            {chr(249),"u","ugrave"},;
            {chr(250),"u","uacute"},;
            {chr(251),"u","ucirc"},;
            {chr(252),"u","uuml"},;
            {chr(253),"y","yacute"},;
            {chr(209),"N","Ntilde"},;
            {chr(241),"n","ntilde"},;
            {chr(199),"C","Ccedil"},;
            {chr(231),"c","ccedil"};
        }
    endif

    __nAccentuation:=len(__aAccentuation)

return(__aAccentuation)
