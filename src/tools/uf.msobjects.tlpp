/*----------------------------------------------------------------------
                        _        _              _
 _ __ ___   ___   ___  | |__    (_)  ___   ___ | |_  ___
| '_ ` _ \ / __| / _ \ | '_ \   | | / _ \ / __|| __|/ __|
| | | | | |\__ \| (_) || |_) |  | ||  __/| (__ | |_ \__ \
|_| |_| |_||___/ \___/ |_.__/  _/ | \___| \___| \__||___/
                              |__/

    The source code `uf.msobjects.tlpp` is based on a more detailed and feature-rich
    source code, `dna.tech.msobjects.tlpp` and implements part of the functionalities
    present in the latter.

*/

#include "totvs.ch"
#include "parmtype.ch"
#include "tlpp-core.th"

namespace uf
using namespace uf

class MsObjects

    static method FindMsObjects(cMsClassOrAttrName as character,oWnd as object) as array
    static method FindMsObjectsClassName(oWnd as object)
    static method FindMsObjectsByAttribute(cAttributeName,oWnd as object) as array

    static method GetOctlFocus(oWnd as object) as object

end class

//------------------------------------------------------------------------------------------------
    /*/
        Funcao:FindMsObjects
        Autor:Marinaldo de Jesus
        Data:17/04/2011
        Uso:Retornar Array com os Objetos conforme cMsClassOrAttrName
        Sintaxe:MsObjects():FindMsObjects(cMsClassOrAttrName,oWnd)
    /*/
//------------------------------------------------------------------------------------------------
static method FindMsObjects(cMsClassOrAttrName,oWnd) CLASS MsObjects
    local aMsObjects as array
    paramtype 1 var cMsClassOrAttrName as character
    paramtype 2 var oWnd as object optional default GetWndDefault()
    aMsObjects:=FindMsObjects(@cMsClassOrAttrName,@oWnd)
    return(aMsObjects)

//------------------------------------------------------------------------------------------------
    /*/
        Funcao:FindMsObjectsClassName
        Autor:Marinaldo de Jesus
        Data:17/04/2011
        Uso:Retornar Array com as Classes dos Objetos utilizados
        Sintaxe:MsObjects():FindMsObjectsClassName(oWnd)
    /*/
//------------------------------------------------------------------------------------------------
static method FindMsObjectsClassName(oWnd) CLASS MsObjects
    local aMsObjects as array
    local lOnlyFindClassName:=.T. as logical
    paramtype 1 var oWnd as object optional default GetWndDefault()
    aMsObjects:=FindMsObjects("GETCLASSNAME",@oWnd,lOnlyFindClassName)
    return(aMsObjects)

//------------------------------------------------------------------------------------------------
    /*/
        Funcao:FindMsObjectsByCaption
        Autor:Marinaldo de Jesus
        Data:17/04/2011
        Uso:Retornar Array com as Classes dos Objetos que contem o Texto na propriedade Caption
        Sintaxe:MsObjects():FindMsObjectsByCaption(cCaptionText,oWnd)
    /*/
//------------------------------------------------------------------------------------------------
static method FindMsObjectsByAttribute(cCaptionText,oWnd) CLASS MsObjects
    local aMsObjects as array
    local lOnlyFindClassName:=.F. as logical
    local lOnlyFindAttributeName:=.T. as logical
    paramtype 1 var cCaptionText as character
    paramtype 1 var oWnd as object optional default GetWndDefault()
    aMsObjects:=FindMsObjects(cCaptionText,@oWnd,lOnlyFindClassName,lOnlyFindAttributeName)
return(aMsObjects)

static function FindMsObjects(cMsClassOrAttrName as character,oWnd as object,lOnlyFindClassName as logical,lOnlyFindAttributeName as logical) as array

    local aMsObjects:=array(0) as array

    local bError as codeblock
    local bErrorBlock as codeblock

    bError:={|e|break(e)}
    bErrorBlock:=ErrorBlock(bError)
    begin sequence

        dfv():Default(@oWnd,GetWndDefault())

        if (ValType(oWnd)!="O")
            break
        endif

        if (ValType(cMsClassOrAttrName)!="C")
            break
        endif

        if (lOnlyFindAttributeName)
            lOnlyFindClassName:=.F.
        endif

        cMsClassOrAttrName:=Upper(cMsClassOrAttrName)
        aMsObjects:=FindObject(@oWnd,@cMsClassOrAttrName,@aMsObjects,@lOnlyFindClassName,@lOnlyFindAttributeName)
        AddObj(@oWnd,@cMsClassOrAttrName,@aMsObjects,@lOnlyFindClassName,@lOnlyFindAttributeName)

    end sequence
    ErrorBlock(bErrorBlock)

    return(aMsObjects)

//------------------------------------------------------------------------------------------------
    /*/
        Funcao:FindObject
        Autor:Marinaldo de Jesus
        Data:17/04/2011
        Uso:Retornar Array com os Objetos conforme cMsClassOrAttrName
    /*/
//------------------------------------------------------------------------------------------------
static function FindObject(oWnd as object,cMsClassOrAttrName as character,aMsObjects as array,lOnlyFindClassName as logical,lOnlyFindAttributeName as logical) as array

    local aChild as array
    local aControls as array

    local bError as codeblock
    local bErrorBlock as codeblock

    local nChild as numeric
    local nChilds as numeric
    local nControl as numeric
    local nControls as numeric

    local oChild as object

    dfv():Default(@aMsObjects,array(0))
    dfv():Default(@lOnlyFindClassName,.F.)
    dfv():Default(@lOnlyFindAttributeName,.F.)

    BEGIN SEQUENCE

        if (!AttIsMemberOf(oWnd,"ACONTROLS",.T./*lRecursive*/))
            break
        endif

        aControls:=oWnd:aControls

        if (aControls==nil)
            AddObj(@oWnd,@cMsClassOrAttrName,@aMsObjects,@lOnlyFindClassName,@lOnlyFindAttributeName)
            if (AttIsMemberOf(oWnd,"OWND",.T./*lRecursive*/))
                oChild:=oWnd:oWnd
                if (oChild!=nil)
                    AddObj(@oChild,@cMsClassOrAttrName,@aMsObjects,@lOnlyFindClassName,@lOnlyFindAttributeName)
                    FindObject(@oChild,@cMsClassOrAttrName,@aMsObjects,@lOnlyFindClassName,@lOnlyFindAttributeName)
                endif
            endif
            break
        endif

        bError:={|e|break(e)}

        nControls:=Len(aControls)
        For nControl:=1 To nControls
            oChild:=aControls[nControl]
            if (oChild==nil)
                Loop
            endif
            AddObj(@oChild,@cMsClassOrAttrName,@aMsObjects,@lOnlyFindClassName,@lOnlyFindAttributeName)
            bErrorBlock:=ErrorBlock(bError)
            begin sequence
                if (!AttIsMemberOf(oChild,"ACONTROLS",.T./*lRecursive*/))
                    break
                endif
                aChild:=oChild:aControls
                if (aChild!=nil)
                    nChilds:=Len(aChild)
                    For nChild:=1 To nChilds
                        oChild:=aChild[nChild]
                        if (oChild==nil)
                            Loop
                        endif
                        AddObj(@oChild,@cMsClassOrAttrName,@aMsObjects,@lOnlyFindClassName,@lOnlyFindAttributeName)
                        FindObject(@oChild,@cMsClassOrAttrName,@aMsObjects,@lOnlyFindClassName,@lOnlyFindAttributeName)
                    Next nChild
                endif
            end sequence
            ErrorBlock(bErrorBlock)
        Next nControl

        if (AttIsMemberOf(oWnd,"OWND",.T./*lRecursive*/))
            oChild:=oWnd:oWnd
            if (oChild!=nil)
                AddObj(@oChild,@cMsClassOrAttrName,@aMsObjects,@lOnlyFindClassName,@lOnlyFindAttributeName)
                FindObject(@oChild,@cMsClassOrAttrName,@aMsObjects,@lOnlyFindClassName,@lOnlyFindAttributeName)
            endif
        endif

    END SEQUENCE

    return(aMsObjects)

//------------------------------------------------------------------------------------------------
    /*/
        Funcao:AddObj
        Autor:Marinaldo de Jesus
        Data:17/04/2011
        Uso:Adicionar o Objeto
    /*/
//------------------------------------------------------------------------------------------------
static function AddObj(oObj as object,cMsClassOrAttrName as character,aMsObjects as array,lOnlyFindClassName as logical,lOnlyFindAttributeName as logical) as logical

    local cClassName as character

    local lAddObj as logical

    dfv():Default(@lOnlyFindClassName,.F.)

    if (lOnlyFindAttributeName)
        lOnlyFindClassName:=.F.
    endif

    cClassName:=Upper(getClassName(oObj))
    if (lOnlyFindAttributeName)
        lAddObj:=AttIsMemberOf(oObj,cMsClassOrAttrName,.T./*lRecursive*/)
        if (lAddObj)
            aAdd(aMsObjects,oObj)
        endif
    elseif (!lOnlyFindClassName)
        lAddObj:=(cClassName==cMsClassOrAttrName)
        if (lAddObj)
            if (lAddObj:=(aScan(aMsObjects,{|oFind|oFind==oObj})==0))
                aAdd(aMsObjects,oObj)
            endif
        endif
    elseif (lAddObj:=(aScan(aMsObjects,{|cClass|cClass==cClassName})==0))
        aAdd(aMsObjects,cClassName)
    endif

    return(lAddObj)

//------------------------------------------------------------------------------------------------
    /*/
        Funcao:GetOctlFocus
        Autor:Marinaldo de Jesus
        Data:26/06/2011
        Uso:Retorna o Objeto Ativo
        Sintaxe:MsObjects():GetOctlFocus(oWnd)
    /*/
//------------------------------------------------------------------------------------------------
static method GetOctlFocus(oWnd) class MsObjects
    paramtype 1 var oWnd as object optional
    return(GetOctlFocus(@oWnd))

static function GetOctlFocus(oWnd) as object

    local bError as codeblock
    local bErrorBlock as codeblock

    local oCtlFocus as object

    bError:={|e|break(e)}
    bErrorBlock:=ErrorBlock(bError)
    begin sequence

        dfv():Default(@oWnd,GetWndDefault())

        if (ValType(oWnd)!="O")
            break
        endif

        if (AttIsMemberOf(oWnd,"OCTLFOCUS",.T./*lRecursive*/))
            oCtlFocus:=oWnd:oCtlFocus
        endif

    end sequence
    ErrorBlock(bErrorBlock)

    return(oCtlFocus)
