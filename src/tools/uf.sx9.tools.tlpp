/*

             ___      _                  _
 ___ __  __ / _ \    | |_   ___    ___  | | ___
/ __|\ \/ /| (_) |   | __| / _ \  / _ \ | |/ __|
\__ \ >  <  \__, | _ | |_ | (_) || (_) || |\__ \
|___//_/\_\   /_/ (_) \__| \___/  \___/ |_||___/

    The source code `uf.sx9.tools.tlpp` is based on a more detailed and feature-rich
    source code, `dna.tech.sx9.tools.tlpp`, and implements part of the functionalities
    present in the latter.

*/

#include "totvs.ch"
#include "parmtype.ch"

namespace uf
using namespace uf

class SX9Tools

    static method getSX9ArrayRelation(cAlias as character) as array
    static method delFromSX9ArrayRelation(cAlias as character,nRecNo as numeric,aArrayRelation as array,bEvalMessage as codeblock) as logical

    static method PutSX9Relation(jSX9Relation as json) as logical

end class

static method getSX9ArrayRelation(cAlias) class SX9Tools

    local aNoRelation as array
    local aArrayRelation:=Array(0) as array

    local cNoRelation as character

    local cX9_DOM as character
    local cX9_CDOM as character
    local cX9_EXPDOM as character
    local cX9_EXPCDOM as character

    local jSX9Relation as json
    local jNoRelation as json

    local lNoRelation as logical

    local nRelation,nRelations as numeric

    paramtype 1 var cAlias as character

    jNoRelation:=JSONObject():New()
    //Verifica as tabelas que nao terao os registros Excluidos
    cNoRelation:=getNewPar("SX9NORL"+cAlias,"")
    aNoRelation:=cTools():StrTokCountArray(cNoRelation,";")
    nRelations:=Len(aNoRelation)
    for nRelation:=1 to nRelations
        jNoRelation[aNoRelation[nRelation]]:=.F.
    next nRelation
    FWFreeArray(@aNoRelation)

    SX9->(dbSetOrder(1))
    if (SX9->(dbSeek(cAlias,.F.)))
        while (SX9->(!eof().and.(X9_DOM==cAlias)))
            cX9_CDOM:=SX9->X9_CDOM
            lNoRelation:=(jNoRelation:HasProperty(cX9_CDOM)).and.(!jNoRelation[cX9_CDOM])
            if (!lNoRelation)
                cX9_DOM:=SX9->X9_DOM
                cX9_EXPDOM:=allTrim(SX9->X9_EXPDOM)
                cX9_EXPCDOM:=allTrim(SX9->X9_EXPCDOM)
                jSX9Relation:=JSONObject():New()
                jSX9Relation["X9_DOM"]:=cX9_DOM
                jSX9Relation["X9_CDOM"]:=cX9_CDOM
                jSX9Relation["X9_EXPDOM"]:=cX9_EXPDOM
                jSX9Relation["X9_EXPCDOM"]:=cX9_EXPCDOM
                jSX9Relation["X9_IDENT"]:=cX9_EXPCDOM
                jSX9Relation["R_E_C_N_O_"]:=SX9->(recNo())
                jSX9Relation["bSeekRelation"]:=&("{||"+cX9_DOM+"->("+cX9_EXPDOM+")}")
                jSX9Relation["bEvalRelation"]:=&("{||"+cX9_DOM+"->("+cX9_EXPDOM+")=="+cX9_CDOM+"->("+cX9_EXPCDOM+")}")
                jSX9Relation["nOrderRelation"]:=RetOrder(cX9_CDOM,cX9_EXPCDOM,.T.)
                aAdd(aArrayRelation,jSX9Relation)
            endif
            SX9->(dbSkip())
        end while

    endif

    FreeObj(@jNoRelation)

return(aArrayRelation)

static method delFromSX9ArrayRelation(cAlias,nRecNo,aArrayRelation,bEvalMessage) class SX9Tools

    local aArea:=FWGetArea() as array
    local aAreaAlias as array
    local aFilBRW_Qry:=Array(0) as array
    local aArrayRelationItem as array

    local bSeekRelation as codeblock
    local bEvalRelation as codeblock

    local cFilBRW_Exp as character
    local cAliasRelation as character

    local lDeleted:=.T. as logical

    local nRelation,nRelations as numeric
    local nOrderRelation as numeric

    paramtype 1 var cAlias as character optional default Alias()
    paramtype 2 var nRecNo as numeric optional default (cAlias)->(recNo())
    paramtype 3 var aArrayRelation as array optional
    paramtype 4 var bEvalMessage as block optional /*{|cAlias|}*/

    aAreaAlias:=(cAlias)->(FWGetArea())

    if (aArrayRelation==nil)
        aArrayRelation:=SX9Tools():getSX9ArrayRelation(@cAlias)
    endif

    //Exclui os Itens das tabelas Filhas
    nRelations:=Len(aArrayRelation)
    for nRelation:=1 to nRelations
        cAliasRelation:=aArrayRelation[nRelation]["X9_CDOM"]
        Eval(bEvalMessage,cAliasRelation)
        nOrderRelation:=aArrayRelation[nRelation]["nOrderRelation"]
        bSeekRelation:=aArrayRelation[nRelation]["bSeekRelation"]
        bEvalRelation:=aArrayRelation[nRelation]["bEvalRelation"]
        if (nOrderRelation==0)
            cFilBRW_Exp:=("'"+Eval(bSeekRelation)+"'=="+aArrayRelation[nRelation]["X9_EXPCDOM"])
            (cAliasRelation)->(FilBrowse(cAliasRelation,@aFilBRW_Qry,cFilBRW_Exp))
            (cAliasRelation)->(dbGoTop())
            lAliasRelation:=(cAliasRelation)->(!eof())
        else
            (cAliasRelation)->(dbSetOrder(nOrderRelation))
            lAliasRelation:=((cAliasRelation)->(dbSeek(Eval(bSeekRelation))))
        endif
        if (lAliasRelation)
            while ((cAliasRelation)->(!eof()).and.Eval(bEvalRelation))
                lDeleted:=((cAliasRelation)->(recLock(cAliasRelation,.F.)))
                if (lDeleted)
                    //Exclui os Itens das tabelas Filhas
                    if (aArrayRelation[nRelation]:HasProperty("aArrayRelation"))
                        aArrayRelationItem:=aArrayRelation[nRelation]["aArrayRelation"]
                    else
                        aArrayRelation[nRelation]["aArrayRelation"]:=SX9Tools():getSX9ArrayRelation(@cAliasRelation)
                        aArrayRelationItem:=aArrayRelation[nRelation]["aArrayRelation"]
                    endif
                    lDeleted:=(cAliasRelation)->(SX9Tools():delFromSX9ArrayRelation(cAliasRelation,RecNo(),@aArrayRelationItem,bEvalMessage))
                    Eval(bEvalMessage,cAliasRelation)
                    if (lDeleted)
                        (cAliasRelation)->(dbDelete())
                    endif
                    (cAliasRelation)->(MsUnLock())
                    if (!lDeleted)
                        exit
                    else
                        (cAliasRelation)->(FkCommit())
                    endif
                else
                    exit
                endif
                (cAliasRelation)->(dbSkip())
            end while
        endif
        if (nOrderRelation==0)
            (cAliasRelation)->(EndFilBrw(cAliasRelation,@aFilBRW_Qry))
            aSize(@aFilBRW_Qry,0)
        endif
        if (!lDeleted)
            exit
        endif
    next nRelation

    Eval(bEvalMessage,cAlias)

    FWRestArea(@aAreaAlias)
    FWRestArea(@aArea)

    FWFreeArray(@aAreaAlias)
    FWFreeArray(@aArea)

    (cAlias)->(MsGoTo(nRecNo))

return(lDeleted)

static method PutSX9Relation(jSX9Relation) class SX9Tools

    local aArea:=FWGetArea() as array
    local aAreaSX9:=SX9->(FWGetArea()) as array

    local cSX9EXPDOM as character

    local lSX9Found as logical
    local lSX9AddNew as logical
    local lPutSX9Relation:=.T. as logical

    local nSX9Ident,nSX9RecNo as numeric

    paramtype 1 var jSX9Relation as json

    lPutSX9Relation:=lPutSX9Relation.and.jSX9Relation:HasProperty("X9_DOM")
    lPutSX9Relation:=lPutSX9Relation.and.jSX9Relation:HasProperty("X9_CDOM")
    lPutSX9Relation:=lPutSX9Relation.and.jSX9Relation:HasProperty("X9_EXPDOM")
    lPutSX9Relation:=lPutSX9Relation.and.jSX9Relation:HasProperty("X9_EXPCDOM")

    if (lPutSX9Relation)
        //X9_DOM
        SX9->(dbSetOrder(1))
        jSX9Relation["X9_DOM"]:=Upper(jSX9Relation["X9_DOM"])
        jSX9Relation["X9_CDOM"]:=Upper(jSX9Relation["X9_CDOM"])
        jSX9Relation["X9_EXPDOM"]:=allTrim(jSX9Relation["X9_EXPDOM"])
        jSX9Relation["X9_EXPCDOM"]:=allTrim(jSX9Relation["X9_EXPCDOM"])
        cSX9EXPDOM:=jSX9Relation["X9_EXPDOM"]
        lSX9Found:=(SX9->(dbSeek(jSX9Relation["X9_DOM"],.F.)))
        nSX9Ident:=0
        while (SX9->(!eof().and.(X9_DOM==jSX9Relation["X9_DOM"])))
            nSX9RecNo:=SX9->(RecNo())
            cSX9Ident:=SX9->X9_IDENT
            if ((jSX9Relation:HasProperty("X9_IDENT")).and.(cSX9Ident==jSX9Relation["X9_IDENT"]))
                lSX9Found:=(SX9->((X9_DOM+X9_CDOM+cSX9Ident)==(jSX9Relation["X9_DOM"]+jSX9Relation["X9_CDOM"]+cSX9Ident)))
            else
                lSX9Found:=(SX9->((X9_DOM+X9_CDOM)==(jSX9Relation["X9_DOM"]+jSX9Relation["X9_CDOM"])))
            endif
            if (lSX9Found)
                lSX9Found:=(Upper(allTrim(SX9->X9_EXPDOM))==jSX9Relation["X9_EXPDOM"])
                lSX9Found:=((lSX9Found).and.(Upper(allTrim(SX9->X9_EXPCDOM))==jSX9Relation["X9_EXPCDOM"]))
                if ((!lSX9Found).and.(jSX9Relation:HasProperty("X9_IDENT")))
                    if (SX9->(recLock("SX9",.F.)))
                        SX9->(dbDelete())
                        SX9->(MsUnLock())
                    endif
                endif
            endif
            nSX9Ident:=Max(nSX9Ident,val(cSX9Ident))
            if (lSX9Found)
                exit
            endif
            SX9->(dbSkip())
        end while
        lSX9AddNew:=(!lSX9Found)
        if (lSX9AddNew)
            cSX9Ident:=__Soma1(StrZero(nSX9Ident,Len(SX9->X9_IDENT)))
        else
            SX9->(MsGoTo(nSX9RecNo))
        endif
        lPutSX9Relation:=(SX9->(recLock("SX9",lSX9AddNew)))
        if (lPutSX9Relation)
            SX9->X9_DOM:=jSX9Relation["X9_DOM"]
            SX9->X9_CDOM:=jSX9Relation["X9_CDOM"]
            SX9->X9_EXPDOM:=cSX9EXPDOM
            SX9->X9_EXPCDOM:=jSX9Relation["X9_EXPCDOM"]
            SX9->X9_IDENT:=cSX9Ident
            SX9->X9_PROPRI:=if(jSX9Relation:HasProperty("X9_PROPRI"),jSX9Relation["X9_EXPCDOM"],"U")
            SX9->X9_LIGDOM:=if(jSX9Relation:HasProperty("X9_LIGDOM"),jSX9Relation["X9_LIGDOM"],"1")
            SX9->X9_LIGCDOM:=if(jSX9Relation:HasProperty("X9_LIGCDOM"),jSX9Relation["X9_LIGCDOM"],"N")
            SX9->X9_CONDSQL:=if(jSX9Relation:HasProperty("X9_CONDSQL"),jSX9Relation["X9_CONDSQL"],"")
            SX9->X9_USEFIL:=if(jSX9Relation:HasProperty("X9_USEFIL"),jSX9Relation["X9_USEFIL"],"S")
            SX9->X9_VINFIL:=if(jSX9Relation:HasProperty("X9_VINFIL"),jSX9Relation["X9_VINFIL"],"2")
            SX9->X9_ENABLE:=if(jSX9Relation:HasProperty("X9_ENABLE"),jSX9Relation["X9_ENABLE"],"S")
            SX9->X9_CHVFOR:=if(jSX9Relation:HasProperty("X9_CHVFOR"),jSX9Relation["X9_CHVFOR"],"2")
            SX9->(MsUnLock())
        endif
    endif

    FWRestArea(@aAreaSX9)
    FWRestArea(@aArea)

    FWFreeArray(@aAreaSX9)
    FWFreeArray(@aArea)

return(lPutSX9Relation)
